varnishtest "Test var vmod"

server s1 {
       rxreq
       txresp
} -start

varnish v1 -vcl+backend {
	import var from "${vmod_topbuild}/src/.libs/libvmod_var.so";

	sub vcl_deliver {
		var.set("foo", "bar");
		set resp.http.x-foo = var.get("foo");
		var.set("foo2", "bar2");
		set resp.http.x-foo = var.get("foo");
		var.clear();
		set resp.http.x-foo2 = "" + var.get("foo2");
		var.set_int("i1", 123);
		set resp.http.i1 = var.get_int("i1") + 1;
		var.set_real("r1", 2);
		var.set_real("r1", var.get_real("r1"));
		set resp.http.r1 = var.get_real("r1");

		var.set_duration("d1", 1m);
		set resp.http.d1 = var.get_duration("d1");

		var.set_ip("ip1", client.ip);
		set resp.http.ip1 = var.get_ip("ip1");

		var.set("e1", "e1");
		if (var.exists("e1")) {
			set resp.http.e1 = "exists";
		}
		if (! var.exists("e2")) {
			set resp.http.e2 = "not exists";
		}
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.http.x-foo == "bar"
	expect resp.http.x-foo2 == ""
	expect resp.http.i1 == "124"
	expect resp.http.r1 == "2.000"
	expect resp.http.d1 == "60.000"
	expect resp.http.ip1 == "127.0.0.1"
	expect resp.http.e1 == "exists"
	expect resp.http.e2 == "not exists"
}

client c1 -run
